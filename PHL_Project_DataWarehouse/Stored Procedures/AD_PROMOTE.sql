USE [DP_Manh]
GO

/****** Object:  StoredProcedure [dbo].[AD_PROMOTE]    Script Date: 12/01/2023 16:06:06 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO





ALTER PROCEDURE [dbo].[AD_PROMOTE] (@varREPORT_MONTH nvarchar(4000), @varCURRENT_REPORT nvarchar(4000)) 
AS 
BEGIN
----format @varREPORT_MONTH chọn tháng báo cáo. vd báo cáo T12 '2022-12-01'
----format @varCURRENT_REPORT chọn tháng đang xét. vd báo cáo T10 '2022-10-01'
----PREPARING DATA
WITH 
AD_STRUCTURE AS (
	SELECT 
		AD_STRUCTURE2.[TERRITORY],
		AD_STRUCTURE1.*
	FROM (SELECT *
		  FROM [SQL_SV65].[PowerBI].[DPO].[Main_AD_STRUCTURE_FULL]
		  WHERE [ExplodedDate]  = @varCURRENT_REPORT) AS AD_STRUCTURE1
	LEFT JOIN [dbo].[T_Main_TERRITORIES] AS AD_STRUCTURE2
	ON AD_STRUCTURE1.[Territory_Code] = AD_STRUCTURE2.[CODE]
)
, FYPANDK2 AS (
	SELECT
		*
	FROM [DP_Manh].[dbo].[T_AD_FYP_K2_MTHLY]
)
, ACTIVE_AGENT AS  (
	SELECT
		*
	FROM [DP_Manh].[dbo].[T_AD_RECRUIT_QTRLY]
)
, AGENCY_STRUCTURE AS (
	SELECT
		*
	FROM [SQL_SV65].[PowerBI].[DPO].[Main_AGENCY_STRUCTURE]
	WHERE [ExplodedDate]  = FORMAT(CAST(@varCURRENT_REPORT AS date), 'yyyyMM')
)
, AD_GIOITHIEU AS (
	SELECT 
		*
	FROM [DP_Manh].[dbo].[T_AD_PROMOTE_GIOITHIEU]
)
----FYP 6 THANG
, A0 AS (
	SELECT
		AD_Code,
		SUM(FYPincTopup) AS FYPincTopup_6M
	FROM FYPANDK2
	WHERE REPORT_MONTH BETWEEN FORMAT(DATEADD(month, -5, @varREPORT_MONTH), 'yyyyMM') 
	AND FORMAT(CAST(@varREPORT_MONTH AS date), 'yyyyMM')
	GROUP BY AD_Code
)
----ACTIVE AGENT 6 THANG
, B0 AS (
	SELECT
		[AD_CODE_FINAL],
		SUM([SUM_TOTAL_ACTIVE_AGENT]) ACTIVE_AGENT_6M
	FROM ACTIVE_AGENT
	WHERE REPORT_MONTH BETWEEN FORMAT(DATEADD(month, -5, @varREPORT_MONTH), 'yyyyMM') 
	AND FORMAT(CAST(@varREPORT_MONTH AS date), 'yyyyMM')
	GROUP BY [AD_CODE_FINAL]
)
----K2 HIEN TAI
, C0 AS (
	SELECT
		AD_Code,
		[K2]
	FROM FYPANDK2
	WHERE REPORT_MONTH = FORMAT(CAST(@varCURRENT_REPORT AS date), 'yyyyMM')
)
----TIEU CHI SO LUONG FM DIRECT HIEN TAI
, D0 AS (
	SELECT
		[AD_Code],
		COUNT(DISTINCT [Agent_Number]) AS SL_FM_DIRECT
	FROM AGENCY_STRUCTURE
	WHERE [GRADE] = 'FM'
		AND [Agent_Name] NOT LIKE 'DUMMY%'
		AND ([Terminated_date] IS NULL OR [Terminated_date] >= DATEADD(month, 1, @varCURRENT_REPORT))
		AND [GM] IS NULL AND [RM] IS NULL AND [DM] IS NULL -- FM TRUC TIEP
	GROUP BY [AD_Code]
)
----TIEU CHI SO LUONG DM HIEN TAI
, E0 AS (
	SELECT
		[AD_Code],
		COUNT(DISTINCT [Agent_Number]) AS SL_DM
	FROM AGENCY_STRUCTURE
	WHERE [GRADE] = 'DM'
		AND [Agent_Name] NOT LIKE 'DUMMY%'
		AND ([Terminated_date] IS NULL OR [Terminated_date] >= DATEADD(month, 1, @varCURRENT_REPORT))
	GROUP BY [AD_Code]
)
----TIEU CHI SO LUONG ZD/SZD CURRENT
, F0 AS (
	SELECT
		TD AS RADandAbove,
		COUNT(CASE WHEN [AD_Grade] IN ('ZD', 'AZD') THEN 1 END) AS SL_ZD, 
		COUNT(CASE WHEN [AD_Grade] IN ('SZD', 'ASZD') THEN 1 END) AS SL_SZD,
		COUNT(CASE WHEN [AD_Grade] IN ('RD', 'ARD') THEN 1 END) AS SL_RD,
		COUNT(CASE WHEN [AD_Grade] IN ('SRD', 'ASRD') THEN 1 END) AS SL_SRD
	FROM AD_STRUCTURE
	WHERE ([Terminated_Date] IS NULL OR [Terminated_Date] >= DATEADD(month, 1, @varCURRENT_REPORT))
	AND [Appointed_Date] < DATEADD(month, 1, @varCURRENT_REPORT)
	GROUP BY TD
	UNION
	SELECT
		TAD AS RADandAbove,
		COUNT(CASE WHEN [AD_Grade] IN ('ZD', 'AZD') THEN 1 END) AS SL_ZD, 
		COUNT(CASE WHEN [AD_Grade] IN ('SZD', 'ASZD') THEN 1 END) AS SL_SZD,
		COUNT(CASE WHEN [AD_Grade] IN ('RD', 'ARD') THEN 1 END) AS SL_RD,
		COUNT(CASE WHEN [AD_Grade] IN ('SRD', 'ASRD') THEN 1 END) AS SL_SRD
	FROM AD_STRUCTURE
	WHERE ([Terminated_Date] IS NULL OR [Terminated_Date] >= DATEADD(month, 1, @varCURRENT_REPORT))
	AND [Appointed_Date] < DATEADD(month, 1, @varCURRENT_REPORT)
	GROUP BY TAD
	UNION
	SELECT
		SRD AS RADandAbove,
		COUNT(CASE WHEN [AD_Grade] IN ('ZD', 'AZD') THEN 1 END) AS SL_ZD, 
		COUNT(CASE WHEN [AD_Grade] IN ('SZD', 'ASZD') THEN 1 END) AS SL_SZD,
		COUNT(CASE WHEN [AD_Grade] IN ('RD', 'ARD') THEN 1 END) AS SL_RD,
		COUNT(CASE WHEN [AD_Grade] IN ('SRD', 'ASRD') THEN 1 END) -1 AS SL_SRD
	FROM AD_STRUCTURE
	WHERE ([Terminated_Date] IS NULL OR [Terminated_Date] >= DATEADD(month, 1, @varCURRENT_REPORT))
	AND [Appointed_Date] < DATEADD(month, 1, @varCURRENT_REPORT)
	GROUP BY SRD
	UNION
	SELECT
		RD AS RADandAbove,
		COUNT(CASE WHEN [AD_Grade] IN ('ZD', 'AZD') THEN 1 END) AS SL_ZD, 
		COUNT(CASE WHEN [AD_Grade] IN ('SZD', 'ASZD') THEN 1 END) AS SL_SZD,
		COUNT(CASE WHEN [AD_Grade] IN ('RD', 'ARD') THEN 1 END) -1 AS SL_RD,
		COUNT(CASE WHEN [AD_Grade] IN ('SRD', 'ASRD') THEN 1 END) AS SL_SRD
	FROM AD_STRUCTURE
	WHERE ([Terminated_Date] IS NULL OR [Terminated_Date] >= DATEADD(month, 1, @varCURRENT_REPORT))
	AND [Appointed_Date] < DATEADD(month, 1, @varCURRENT_REPORT)
	GROUP BY RD
	UNION
	SELECT
		RAD AS RADandAbove,
		COUNT(CASE WHEN [AD_Grade] IN ('ZD', 'AZD') THEN 1 END) AS SL_ZD, 
		COUNT(CASE WHEN [AD_Grade] IN ('SZD', 'ASZD') THEN 1 END) AS SL_SZD,
		COUNT(CASE WHEN [AD_Grade] IN ('RD', 'ARD') THEN 1 END) AS SL_RD,
		COUNT(CASE WHEN [AD_Grade] IN ('SRD', 'ASRD') THEN 1 END) AS SL_SRD
	FROM AD_STRUCTURE
	WHERE ([Terminated_Date] IS NULL OR [Terminated_Date] >= DATEADD(month, 1, @varCURRENT_REPORT))
	AND [Appointed_Date] < DATEADD(month, 1, @varCURRENT_REPORT)
	GROUP BY RAD
)
----CONG THEM TU NGUOI DUOC GIOI THIEU
, F1 AS (
	SELECT
		F11.[AD_CODE_NGT],
		F11.[VI_TRI_NGT],
		F15.[AD_Grade] AS [VI_TRI_NGT_CURRENT],
		F11.[AD_CODE_NGUOIDCTUYEN],
		F11.[VI_TRI_NGUOIDCTUYEN],
		F14.[AD_Grade] AS [VI_TRI_NGUOIDCTUYEN_CURRENT],
		F11.[FROM_DM],
		F12.FYPincTopup_6M AS FYPincTopup_6M_NGUOIDCTUYEN,
		CASE
			WHEN F15.[AD_Grade] IN ('SZD', 'ASZD') AND F11.[FROM_DM] = 'Y' THEN F12.FYPincTopup_6M
			WHEN F15.[AD_Grade] NOT IN ('ZD', 'AZD') AND F11.[FROM_DM] <> 'Y' THEN F12.FYPincTopup_6M * 0.5
		END AS FYP_NGT_DC_CONG_THEM,
		F13.SL_DM AS SL_DM_NGUOIDCTUYEN,
		CASE
			WHEN F15.[AD_Grade] IN ('SZD', 'ASZD')
				AND F14.[AD_Grade] IN ('ZD', 'AZD')
			THEN F13.SL_DM
			ELSE 0
		END AS SL_DM_NGT_DC_CONG_THEM,
		CASE
			WHEN F15.[AD_Grade] IN ('SZD', 'ASZD')
				AND F14.[AD_Grade] IN ('ZD', 'AZD')
			THEN 1
			ELSE 0
		END AS SL_ZD_CONG_THEM_CHO_SZD,
		F14.[Terminated_Date] AS [Terminated_Date_NGUOIDCTUYEN]
	FROM AD_GIOITHIEU AS F11 LEFT JOIN A0 AS F12
	ON F11.[AD_CODE_NGUOIDCTUYEN] = F12.AD_Code
	LEFT JOIN E0 AS F13
	ON F11.[AD_CODE_NGUOIDCTUYEN] = F13.[AD_Code]
	LEFT JOIN AD_STRUCTURE AS F14
	ON F11.[AD_CODE_NGUOIDCTUYEN] = F14.[AD_Code]
	LEFT JOIN AD_STRUCTURE AS F15
	ON F11.[AD_CODE_NGT] = F15.[AD_Code]
)
, F2 AS (
	SELECT 
		AD_CODE_NGT,
		VI_TRI_NGT_CURRENT,
		AD_CODE_NGUOIDCTUYEN,
		VI_TRI_NGUOIDCTUYEN,
		ISNULL(FYP_NGT_DC_CONG_THEM, 0) AS FYP_NGT_DC_CONG_THEM,
		ISNULL(SL_DM_NGT_DC_CONG_THEM, 0) AS SL_DM_NGT_DC_CONG_THEM,
		ISNULL(SL_ZD_CONG_THEM_CHO_SZD, 0) AS SL_ZD_CONG_THEM_CHO_SZD,
		[Terminated_Date_NGUOIDCTUYEN]
	FROM F1
)
, F3 AS (
	SELECT
		[AD_CODE_NGT],
		SUM(FYP_NGT_DC_CONG_THEM) AS FYP_NGT_DC_CONG_THEM,
		SUM(SL_DM_NGT_DC_CONG_THEM) AS SL_DM_NGT_DC_CONG_THEM,
		SUM(SL_ZD_CONG_THEM_CHO_SZD) AS SL_ZD_CONG_THEM_CHO_SZD
	FROM F2
	WHERE [Terminated_Date_NGUOIDCTUYEN] IS NULL
	GROUP BY [AD_CODE_NGT]
)

----TONG HOP KET QUA
, G0 AS (
	SELECT
		G01.TERRITORY,
		G01.[AD_Code],
		G01.[ADName],
		G01.[AD_Grade],
		IIF(G01.[DemotePromote_Date] IS NOT NULL, G01.[DemotePromote_Date], 
			DATEADD(month, 2, G01.[Appointed_Date])) AS NGAY_GIU_CHUC_VU,
		ISNULL(G02.FYPincTopup_6M, 0) AS FYPincTopup_6M,
		ISNULL(G08.FYP_NGT_DC_CONG_THEM, 0) AS FYP_NGT_DC_CONG_THEM,
		ISNULL(G03.ACTIVE_AGENT_6M, 0) AS ACTIVE_AGENT_6M,
		ISNULL(G05.SL_FM_DIRECT, 0) AS SL_FM_DIRECT,
		ISNULL(G06.SL_DM, 0) AS SL_DM,
		ISNULL(G08.SL_DM_NGT_DC_CONG_THEM, 0) AS SL_DM_NGT_DC_CONG_THEM,
		ISNULL(G07.SL_ZD, 0) + ISNULL(G08.SL_ZD_CONG_THEM_CHO_SZD, 0) AS SL_ZD,
		ISNULL(G07.SL_SZD, 0) AS SL_SZD,
		ISNULL(G07.SL_RD, 0) AS SL_RD,
		ISNULL(G07.SL_SRD, 0) AS SL_SRD,
		G04.K2
	FROM AD_STRUCTURE AS G01 LEFT JOIN A0 AS G02 ON G01.[AD_Code] = G02.AD_Code
	LEFT JOIN B0 AS G03  ON G01.[AD_Code] = G03.AD_CODE_FINAL
	LEFT JOIN C0 AS G04 ON G01.[AD_Code] = G04.AD_Code
	LEFT JOIN D0 AS G05 ON G01.[AD_Code] = G05.[AD_Code]
	LEFT JOIN E0 AS G06 ON G01.[AD_Code] = G06.[AD_Code]
	LEFT JOIN F0 AS G07 ON G01.[AD_Code] = G07.RADandAbove
	LEFT JOIN F3 AS G08 ON G01.[AD_Code] = G08.AD_CODE_NGT
	WHERE G01.[Last_Status] <> 'Terminated'
)

SELECT
	TERRITORY,
	[AD_Code],
	[ADName],
	[AD_Grade],
	NGAY_GIU_CHUC_VU,
	FYPincTopup_6M,
	FYP_NGT_DC_CONG_THEM,
	(FYPincTopup_6M + FYP_NGT_DC_CONG_THEM) AS FYP_XET_THANG_CAP,
	ACTIVE_AGENT_6M,
	SL_FM_DIRECT,
	SL_DM,
	SL_DM_NGT_DC_CONG_THEM,
	(SL_DM + SL_DM_NGT_DC_CONG_THEM) AS SL_DM_XET_THANG_CAP,
	SL_ZD,
	SL_SZD,
	SL_RD,
	SL_SRD,
	K2
FROM G0

END;
GO


